import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'application'
    id 'com.diffplug.spotless' version '6.6.1'
    id "de.undercouch.download" version "5.1.0"
}

ext {
    kafkaVersion = '3.2.0'
    slf4jVersion = '1.7.36'
    jupiterVersion = '5.8.2'
    prometheusJavaagentVersion = '0.16.0'
}

group = 'com.yushkevich'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

apply plugin: 'java'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'de.undercouch.download'

task downloadJavaAgent(type: Download) {
    src "https://repo1.maven.org/maven2/io/prometheus/jmx/jmx_prometheus_javaagent/${prometheusJavaagentVersion}/jmx_prometheus_javaagent-${prometheusJavaagentVersion}.jar"
    dest 'build/libs'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: "${kafkaVersion}"
    implementation group: 'org.apache.kafka', name: 'kafka-streams', version: "${kafkaVersion}"
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: "${slf4jVersion}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${jupiterVersion}"
    testImplementation group: 'org.apache.kafka', name: 'kafka-streams-test-utils', version: "${kafkaVersion}"
}

application {
    mainClass = 'com.yushkevich.emnify.KafkaStreamsChallengeApplication'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes(
                'Main-Class': 'com.yushkevich.emnify.KafkaStreamsChallengeApplication'
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

build.dependsOn downloadJavaAgent